PREFIX rdf:          <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcterms:      <http://purl.org/dc/terms/>
PREFIX miv:          <http://www.wegenenverkeer.be/ns/verkeersmetingen#>
PREFIX xsd:          <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?sub rdf:type miv:Meetpunt .
    ?sub miv:unieke_id ?unieke_id .
    ?sub miv:beschrijvende_id ?beschrijvende_id .
    ?sub miv:volledige_naam ?volledige_naam .
    ?sub miv:Ident_8 ?Ident_8 .
    ?sub miv:lve_nr ?lve_nr .
    ?sub miv:Rijstrook ?Rijstrook .
    ?sub dcterms:modified ?modified .
    ?sub miv:Kmp_Rsys ?Kmp_Rsys_fixed .
    ?sub miv:X_coord_EPSG_31370 ?X_coord_EPSG_31370_fixed .
    ?sub miv:Y_coord_EPSG_31370 ?Y_coord_EPSG_31370_fixed .
    ?sub miv:lengtegraad_EPSG_4326 ?lengtegraad_EPSG_4326_fixed .
    ?sub miv:breedtegraad_EPSG_4326 ?breedtegraad_EPSG_4326_fixed .
}
WHERE {
    ?sub rdf:type miv:Meetpunt .
    ?sub miv:unieke_id ?unieke_id .
    ?sub miv:beschrijvende_id ?beschrijvende_id .
    ?sub miv:volledige_naam ?volledige_naam .
    ?sub miv:Ident_8 ?Ident_8 .
    ?sub miv:lve_nr ?lve_nr .
    ?sub miv:Rijstrook ?Rijstrook .
    ?sub dcterms:modified ?modified .
    optional {?sub miv:Kmp_Rsys ?Kmp_Rsys }
    ?sub miv:X_coord_EPSG_31370 ?X_coord_EPSG_31370 .
    ?sub miv:Y_coord_EPSG_31370 ?Y_coord_EPSG_31370 .
    ?sub miv:lengtegraad_EPSG_4326 ?lengtegraad_EPSG_4326 .
    ?sub miv:breedtegraad_EPSG_4326 ?breedtegraad_EPSG_4326 .
    bind(strdt(if(!bound(?Kmp_Rsys) || ?Kmp_Rsys = "NULL" || ?Kmp_Rsys = "", "0", replace(?Kmp_Rsys, ",", ".")), xsd:double) as ?Kmp_Rsys_fixed) .
    bind(replace(?X_coord_EPSG_31370, "," , ".") as ?X_coord_EPSG_31370_fixed) .
    bind(replace(?Y_coord_EPSG_31370, "," , ".") as ?Y_coord_EPSG_31370_fixed) .
    bind(replace(?lengtegraad_EPSG_4326, "," , ".") as ?lengtegraad_EPSG_4326_fixed) .
    bind(replace(?breedtegraad_EPSG_4326, "," , ".") as ?breedtegraad_EPSG_4326_fixed) .
}